// Limpa variável
operation clear(A){
    1: if zero A then goto 0 else goto 2
    2: do dec A goto 1
}

// A += B (soma destrutiva)
operation sumDest(A,B){
    1: if zero B then goto 0 else goto 2
    2: do dec B goto 3
    3: do inc A goto 1
}

// A = B (atribuição destrutiva)
operation movDest(A,B){
    1: do clear(A) goto 2
    2: do sumDest(A,B) goto 0
}

// A += B usando C
operation sum(A,B,C){
    1: if zero B then goto 5 else goto 2
    2: do dec  B goto 3
    3: do inc  A goto 4
    4: do inc  C goto 1
    5: do sumDest(B,C) goto 0
}

// A = B usando C (atribuição não-destrutiva)
operation mov(A,B,C){
    1: do clear(A) goto 2
    2: do sum(A,B,C) goto 0
}

// A % 2 =? 0
test divBy2(A,C){
    1: do movDest(C,A) goto 2
    2: if zero C then goto true else goto 3
    3: do dec C goto 4
    4: do inc A goto 5
    5: if zero C then goto false else goto 6
    6: do dec C goto 7
    7: do inc A goto 2
}

// A = A // 2 usando C
operation div2(A,C){
    1: do movDest(C,A) goto 2
    2: if zero C then goto 0 else goto 3
    3: do dec C goto 4
    4: if zero C then goto 0 else goto 5
    5: do dec C goto 6
    6: do inc A goto 2
}

// Decodificação
operation decod(C){
    1: do mov(D,C,E) goto 2
    2: if zero D then goto 0 else goto 3
    3: if divBy2(D,E) then goto 4 else goto 6
    4: do div2(D,E) goto 5
    5: do inc A goto 3
    6: do dec D goto 7
    7: do div2(D,E) goto 8
    8: do movDest(B,D) goto 0
}

// Programa principal
main {
    1: do movDest(C,X) goto 2
    2: do decod(C) goto 0
}